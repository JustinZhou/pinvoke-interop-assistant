' Copyright (c) Microsoft Corporation.  All rights reserved.
'The following code was generated by Microsoft Visual Studio 2005.
'The test owner should check each test for validity.
Imports System
Imports System.Text
Imports System.Collections.Generic
Imports PInvoke
Imports PInvoke.Parser
Imports Xunit


'''<summary>
'''This is a test class for PInvoke.Parser.NativeTypeBag and is intended
'''to contain all PInvoke.Parser.NativeTypeBag Unit Tests
'''</summary>
Public Class NativeSymbolBagTest

    '''<summary>
    '''A test for AddDefinedType(ByVal PInvoke.Parser.NativeDefinedType)
    '''</summary>
    <Fact>
    Public Sub AddDefinedTypeTest()
        Dim bag As New NativeSymbolBag()
        Dim definedNt1 As New NativeStruct("s1")
        bag.AddDefinedType(definedNt1)

        Dim ret1 As NativeType = Nothing
        Assert.True(bag.TryFindOrLoadNativeType(definedNt1.DisplayName, ret1))
        Assert.Same(ret1, definedNt1)
    End Sub

    <Fact>
    Public Sub AddDefinedTypeTest2()
        Dim bag As New NativeSymbolBag()
        Dim definedNt1 As New NativeStruct("s1")
        Dim definedNt2 As New NativeStruct("s2")
        bag.AddDefinedType(definedNt1)
        bag.AddDefinedType(definedNt2)

        Dim ret1 As NativeType = Nothing
        Dim ret2 As NativeType = Nothing
        Assert.True(bag.TryFindOrLoadNativeType(definedNt1.DisplayName, ret1))
        Assert.True(bag.TryFindOrLoadNativeType(definedNt2.DisplayName, ret2))
        Assert.Same(ret1, definedNt1)
        Assert.Same(ret2, definedNt2)
    End Sub

    ''' <summary>
    ''' Adding the same type twice should throw an exception
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub AddDefinedTypeTest3()
        Dim bag As New NativeSymbolBag()
        Dim definedNt1 As New NativeStruct("s1")
        Dim definedNt2 As New NativeStruct("s1")
        bag.AddDefinedType(definedNt1)
        Assert.Throws(Of ArgumentException)(Sub() bag.AddDefinedType(definedNt2))
    End Sub

    <Fact>
    Public Sub AddTypeDef1()
        Dim bag As New NativeSymbolBag()
        Dim td1 As New NativeTypeDef("td1")

        bag.AddTypedef(td1)

        Dim ret1 As NativeType = Nothing
        Assert.True(bag.TryFindOrLoadNativeType(td1.DisplayName, ret1))
        Assert.Same(ret1, td1)

    End Sub

    <Fact>
    Public Sub AddTypeDef2()
        Dim bag As New NativeSymbolBag()
        Dim td1 As New NativeTypeDef("td1")
        Dim td2 As New NativeTypeDef("td2")

        bag.AddTypedef(td1)
        bag.AddTypedef(td2)

        Dim ret1 As NativeType = Nothing
        Dim ret2 As NativeType = Nothing
        Assert.True(bag.TryFindOrLoadNativeType(td1.DisplayName, ret1))
        Assert.Same(ret1, td1)
        Assert.True(bag.TryFindOrLoadNativeType(td2.DisplayName, ret2))
        Assert.Same(ret2, td2)
    End Sub

    ''' <summary>
    ''' adding the same typedefe twice should throw an exception
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub AddTypeDef3()
        Dim bag As New NativeSymbolBag()
        Dim td1 As New NativeTypeDef("td1")
        Dim td2 As New NativeTypeDef("td1")

        bag.AddTypedef(td1)
        Assert.Throws(Of ArgumentException)(Sub() bag.AddTypedef(td2))
    End Sub

    <Fact>
    Public Sub AddMixed1()
        Dim bag As New NativeSymbolBag()
        Dim definedNt1 As New NativeStruct("s1")
        bag.AddDefinedType(definedNt1)
        Dim td1 As New NativeTypeDef("td1")
        bag.AddTypedef(td1)

        Dim ret As NativeType = Nothing
        Assert.True(bag.TryFindOrLoadNativeType(definedNt1.DisplayName, ret))
        Assert.Same(definedNt1, ret)
        Assert.True(bag.TryFindOrLoadNativeType(td1.DisplayName, ret))
        Assert.Same(td1, ret)

    End Sub

    ''' <summary>
    ''' TypeDefs and NativedefinedTypes can have the same full name.  Doesn't make
    ''' much sense in the real world but it's allowed for flexibility in the parser
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub AddMixed2()
        Dim bag As New NativeSymbolBag()
        Dim definedNt1 As New NativeStruct("s1")
        bag.AddDefinedType(definedNt1)
        Dim td1 As New NativeTypeDef("s1")
        bag.AddTypedef(td1)

        Dim ret As NativeType = Nothing
        Assert.True(bag.TryFindOrLoadNativeType(definedNt1.DisplayName, ret))
        Assert.Same(definedNt1, ret)
        Assert.True(bag.TryFindOrLoadNativeType(td1.DisplayName, ret))
        Assert.Same(definedNt1, ret)
    End Sub

    ''' <summary>
    ''' Resolve a simple type def
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Resolve1()
        Dim bag As New NativeSymbolBag()
        Dim s1 As New NativeStruct("s1")
        bag.AddDefinedType(s1)

        Dim td1 As New NativeTypeDef("td1")
        Dim n1 As New NativeNamedType("s1")
        td1.RealType = n1
        bag.AddTypedef(td1)

        Assert.True(bag.TryResolveSymbolsAndValues())
        Assert.Same(s1, n1.RealType)
    End Sub

    ''' <summary>
    ''' Resolve a pointer type
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Resolve2()
        Dim bag As New NativeSymbolBag()
        Dim s1 As New NativeStruct("s1")
        bag.AddDefinedType(s1)

        Dim td1 As New NativeTypeDef("td1")
        Dim n1 As New NativeNamedType("s1")
        Dim p1 As New NativePointer(n1)
        td1.RealType = p1
        bag.AddTypedef(td1)

        Assert.True(bag.TryResolveSymbolsAndValues())
        Assert.Same(s1, n1.RealType)
    End Sub

    ''' <summary>
    ''' Simple proc add
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Proc1()
        Dim bag As New NativeSymbolBag()

        Dim p1 As New NativeProcedure("p1")
        p1.Signature.ReturnType = New NativeBuiltinType(BuiltinType.NativeDouble)

        bag.AddProcedure(p1)
        Assert.True(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' Resolve a procedure with a simple parameter
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Proc2()
        Dim bag As New NativeSymbolBag()

        Dim p1 As New NativeProcedure("p1")
        p1.Signature.ReturnType = New NativeBuiltinType(BuiltinType.NativeDouble)
        p1.Signature.Parameters.Add(New NativeParameter("param1", New NativeBuiltinType(BuiltinType.NativeDouble)))

        bag.AddProcedure(p1)
        Assert.True(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' Unresolvable parameter
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Proc3()
        Dim bag As New NativeSymbolBag()

        Dim p1 As New NativeProcedure("p1")
        p1.Signature.ReturnType = New NativeBuiltinType(BuiltinType.NativeDouble)
        p1.Signature.Parameters.Add(New NativeParameter("param1", New NativeNamedType("foo")))

        bag.AddProcedure(p1)
        Assert.False(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' Unresolavable return type
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Proc4()
        Dim bag As New NativeSymbolBag()

        Dim p1 As New NativeProcedure("p1")
        p1.Signature.ReturnType = New NativeNamedType("foo")
        p1.Signature.Parameters.Add(New NativeParameter("param1", New NativeBuiltinType(BuiltinType.NativeDouble)))

        bag.AddProcedure(p1)
        Assert.False(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' resolve a named return type 
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Proc5()
        Dim bag As New NativeSymbolBag()

        Dim p1 As New NativeProcedure("p1")
        p1.Signature.ReturnType = New NativeNamedType("foo")
        p1.Signature.Parameters.Add(New NativeParameter("param1", New NativeBuiltinType(BuiltinType.NativeDouble)))

        bag.AddProcedure(p1)
        bag.AddTypedef(New NativeTypeDef("foo", New NativeBuiltinType(BuiltinType.NativeFloat)))
        Assert.True(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' resolve a named parameter
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Proc6()
        Dim bag As New NativeSymbolBag()

        Dim p1 As New NativeProcedure("p1")
        p1.Signature.ReturnType = New NativeBuiltinType(BuiltinType.NativeInt32, True)
        p1.Signature.Parameters.Add(New NativeParameter("param1", New NativeNamedType("foo")))

        bag.AddProcedure(p1)
        bag.AddTypedef(New NativeTypeDef("foo", New NativeBuiltinType(BuiltinType.NativeFloat)))
        Assert.True(bag.TryResolveSymbolsAndValues())
    End Sub

    <Fact>
    Public Sub FindOrLoad1()
        Dim ns As NativeStorage = NativeStorage.DefaultInstance
        ns.AddDefinedType(New NativeStruct("s1"))
        Dim bag As New NativeSymbolBag(ns)

        Dim s1 As NativeDefinedType = Nothing
        Assert.False(bag.TryFindDefinedType("s1", s1))
        Assert.True(bag.TryFindOrLoadDefinedType("s1", s1))
        Assert.True(bag.TryFindDefinedType("s1", s1))
    End Sub

    <Fact>
    Public Sub FindOrLoad2()
        Dim ns As NativeStorage = NativeStorage.DefaultInstance
        ns.AddTypedef(New NativeTypeDef("td", New NativeBuiltinType(BuiltinType.NativeChar)))
        Dim bag As New NativeSymbolBag(ns)

        Dim td As NativeTypeDef = Nothing
        Assert.False(bag.TryFindTypedef("td", td))
        Assert.True(bag.TryFindOrLoadTypedef("td", td))
        Assert.True(bag.TryFindTypedef("td", td))
    End Sub

    <Fact>
    Public Sub FindOrLoad3()
        Dim ns As NativeStorage = NativeStorage.DefaultInstance
        ns.AddConstant(New NativeConstant("c1", "value"))
        Dim bag As New NativeSymbolBag(ns)

        Dim c As NativeConstant = Nothing
        Assert.False(bag.TryFindConstant("c1", c))
        Assert.True(bag.TryFindOrLoadConstant("c1", c))
        Assert.True(bag.TryFindConstant("c1", c))
    End Sub

    <Fact>
    Public Sub FindOrLoad4()
        Dim ns As NativeStorage = NativeStorage.DefaultInstance
        Dim p1 As New NativeProcedure("p1")
        p1.Signature.ReturnType = New NativeBuiltinType(BuiltinType.NativeBoolean)
        ns.AddProcedure(p1)
        Dim bag As New NativeSymbolBag(ns)

        Dim p As NativeProcedure = Nothing
        Assert.False(bag.TryFindProcedure("p1", p))
        Assert.True(bag.TryFindOrLoadProcedure("p1", p))
        Assert.True(bag.TryFindProcedure("p1", p))
    End Sub

    ''' <summary>
    ''' Make sure we can load the cases where the user does a 
    ''' "typedef struct foo foo"
    ''' 
    ''' This is a common C practice and we need to be able to resolve this correctly
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub FindOrLoad5()
        Dim bag As New NativeSymbolBag()
        bag.AddTypedef(New NativeTypeDef("foo", New NativeNamedType("struct", "foo")))
        bag.AddDefinedType(New NativeStruct("foo"))

        Dim nt As NativeType = Nothing
        Assert.True(bag.TryFindOrLoadNativeType(New NativeNamedType("struct", "foo"), nt))
        Assert.Equal(NativeSymbolKind.StructType, nt.Kind)
    End Sub

    ''' <summary>
    ''' When a type is loaded during a resolve it should be added
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub ResolveLoad1()
        Dim ns As New NativeStorage
        ns.AddDefinedType(New NativeStruct("s1"))

        Dim bag As New NativeSymbolBag(ns)
        bag.AddTypedef(New NativeTypeDef("S1", "s1"))
        Assert.True(bag.TryResolveSymbolsAndValues())

        Dim td As NativeDefinedType = Nothing
        Assert.True(bag.TryFindDefinedType("s1", td))
    End Sub

    <Fact>
    Public Sub ResolveLoad2()
        Dim ns As New NativeStorage
        ns.AddTypedef(New NativeTypeDef("TEST_INT", BuiltinType.NativeInt32))

        Dim s1 As New NativeStruct("s1")
        s1.Members.Add(New NativeMember("m1", New NativeNamedType("TEST_INT")))

        Dim bag As New NativeSymbolBag(ns)
        bag.AddDefinedType(s1)
        Assert.True(bag.TryResolveSymbolsAndValues())

        Dim td As NativeTypeDef = Nothing
        Assert.True(bag.TryFindTypedef("TEST_INT", td))
    End Sub


    <Fact>
    Public Sub Anonymous1()
        Dim name As String = NativeSymbolBag.GenerateAnonymousName()
        Assert.True(NativeSymbolBag.IsAnonymousName(name))
    End Sub

    <Fact>
    Public Sub Anonymous2()
        Assert.False(NativeSymbolBag.IsAnonymousName("foo"))
    End Sub

    <Fact>
    Public Sub AnonymousType1()
        Dim nt As New NativeStruct()
        nt.IsAnonymous = True
        Assert.True(String.IsNullOrEmpty(nt.Name))

        Dim bag As New NativeSymbolBag()
        bag.AddDefinedType(nt)
        Assert.True(NativeSymbolBag.IsAnonymousName(nt.Name))
    End Sub

    ''' <summary>
    ''' Make sure we can load a type that has a recursive reference to itself
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Storage1()
        Dim bag As New NativeSymbolBag()
        bag.NativeStorageLookup = StorageFactory.CreateStandard()

        Dim nt As NativeType = Nothing
        Assert.True(bag.TryFindOrLoadNativeType("RecursiveStruct", nt))
    End Sub

    ''' <summary>
    ''' Use one const to resolve the other
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Value1()
        Dim bag As New NativeSymbolBag()
        bag.AddConstant(New NativeConstant("foo", "1"))
        bag.AddConstant(New NativeConstant("bar", "foo+2"))

        Assert.Equal(1, bag.FindUnresolvedNativeValues().Count)
        Assert.True(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' Resolve one enum value against the other
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Value2()
        Dim bag As New NativeSymbolBag()
        Dim ntEnum As NativeEnum = New NativeEnum("Enum1")
        ntEnum.Values.Add(New NativeEnumValue("v1", "1"))
        ntEnum.Values.Add(New NativeEnumValue("v2", "v1+1"))
        bag.AddDefinedType(ntEnum)
        Assert.Equal(1, bag.FindUnresolvedNativeValues().Count)
        Assert.True(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' Resolve a cast expression type
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Value3()
        Dim bag As New NativeSymbolBag()
        Dim ntStruct1 As New NativeStruct("s1")
        bag.AddDefinedType(ntStruct1)

        Dim ntConst1 As New NativeConstant("c1", "(s1)1")
        bag.AddConstant(ntConst1)

        Assert.Equal(1, bag.FindUnresolvedNativeValues().Count)
        Assert.True(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' Resolve a constant value
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Value4()
        Dim bag As New NativeSymbolBag()
        Dim ntConst1 As New NativeConstant("c1", "1")
        bag.AddConstant(ntConst1)
        Dim ntConst2 As New NativeConstant("c2", "5+c1")
        bag.AddConstant(ntConst2)

        Assert.Equal(1, bag.FindUnresolvedNativeValues().Count)
        Assert.True(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' Make sure that a SymbolValue can be properly loaded from storage
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub ValueFromStorage1()
        Dim bag As New NativeSymbolBag(New NativeStorage())
        bag.NativeStorageLookup.AddConstant(New NativeConstant("c1", "1"))
        Dim ntConst2 As New NativeConstant("c2", "5+c1")
        bag.AddConstant(ntConst2)

        Assert.Equal(1, bag.FindUnresolvedNativeValues().Count)
        Assert.True(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' Make sure an enum value can be loaded from storage
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub ValueFromStorage2()
        Dim bag As New NativeSymbolBag()

        Dim ntEnum As New NativeEnum("e1")
        ntEnum.Values.Add(New NativeEnumValue("v1", "5"))
        bag.NativeStorageLookup.AddDefinedType(ntEnum)

        Dim ntConst1 As New NativeConstant("c1", "5+v1")
        bag.AddConstant(ntConst1)

        Assert.Equal(1, bag.FindUnresolvedNativeValues().Count)
        Assert.True(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' Make sure cast expression types can be loaded from storage
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub ValueFromStorage3()
        Dim bag As New NativeSymbolBag(New NativeStorage())
        bag.NativeStorageLookup.AddDefinedType(New NativeStruct("s1"))

        Dim ntConst1 As New NativeConstant("c1", "(s1)1")
        bag.AddConstant(ntConst1)

        Assert.Equal(1, bag.FindUnresolvedNativeValues().Count)
        Assert.True(bag.TryResolveSymbolsAndValues())
    End Sub

    ''' <summary>
    ''' Make sure that opaque types are resolved
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Opaque1()
        Dim named As New NativeNamedType("struct", "foo")
        Dim ptr As New NativePointer(named)
        Dim td As New NativeTypeDef("FOOBAR", ptr)
        Dim bag As New NativeSymbolBag()

        bag.AddTypedef(td)
        Assert.Equal(1, bag.FindUnresolvedNativeSymbolRelationships().Count)
        Assert.True(bag.TryResolveSymbolsAndValues())
        Assert.NotNull(named.RealType)
        Assert.Equal(NativeSymbolKind.OpaqueType, named.RealType.Kind)
    End Sub

    ''' <summary>
    ''' Don't resolve an unqualified type name to an opaque type 
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub Opaque2()
        Dim named As New NativeNamedType("foo")
        Dim ptr As New NativePointer(named)
        Dim td As New NativeTypeDef("FOOBAR", ptr)
        Dim bag As New NativeSymbolBag()

        bag.AddTypedef(td)
        Assert.Equal(1, bag.FindUnresolvedNativeSymbolRelationships().Count)
        Assert.False(bag.TryResolveSymbolsAndValues())
    End Sub

End Class
