' Copyright (c) Microsoft Corporation.  All rights reserved.
'The following code was generated by Microsoft Visual Studio 2005.
'The test owner should check each test for validity.
Imports System
Imports System.Text
Imports System.Collections.Generic
Imports PInvoke
Imports PInvoke.Transform
Imports Xunit

'''<summary>
'''This is a test class for PInvoke.NativeSymbolTransform and is intended
'''to contain all PInvoke.NativeSymbolTransform Unit Tests
'''</summary>
Public Class NativeSymbolTransformTest

    Private Function Print(ByVal ns As NativeSymbol) As String
        If ns Is Nothing Then
            Return "<Nothing>"
        End If

        Dim str As String = ns.Name
        For Each child As NativeSymbol In ns.GetChildren()
            str &= "(" & Print(child) & ")"
        Next

        Return str
    End Function

    Private Sub VerifyTree(ByVal ns As NativeSymbol, ByVal str As String)
        Dim realStr As String = Print(ns)
        Assert.Equal(str, realStr)
    End Sub

    <Fact>
    Public Sub CollapseNamed()
        Dim s1 As New NativeStruct("s1")
        s1.Members.Add(New NativeMember(
            "m1",
            New NativeNamedType(
                "char",
                New NativeBuiltinType(BuiltinType.NativeChar))))
        VerifyTree(s1, "s1(m1(char(char)))")

        Dim transform As New NativeSymbolTransform
        transform.CollapseNamedTypes(s1)
        VerifyTree(s1, "s1(m1(char))")
    End Sub

    ''' <summary>
    ''' Collapsing a null type shouldn't do anything
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub CallapseNamed2()
        Dim s1 As New NativeStruct("s1")
        s1.Members.Add(New NativeMember(
            "m1",
            New NativeNamedType("char")))
        VerifyTree(s1, "s1(m1(char))")

        Dim transform As New NativeSymbolTransform
        transform.CollapseNamedTypes(s1)
        VerifyTree(s1, "s1(m1(char))")
    End Sub

    <Fact>
    Public Sub CollapseTypedef1()
        Dim s1 As New NativeStruct("s1")
        s1.Members.Add(New NativeMember(
            "m1",
            New NativeTypeDef(
                "PCHAR",
                New NativePointer(New NativeBuiltinType(BuiltinType.NativeChar)))))
        VerifyTree(s1, "s1(m1(PCHAR(*(char))))")

        Dim transform As New NativeSymbolTransform
        transform.CollapseTypedefs(s1)
        VerifyTree(s1, "s1(m1(*(char)))")
    End Sub

    ''' <summary>
    ''' Collapsing a null typedef shouldn't do anything
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub CollapseTypedef2()
        Dim s1 As New NativeStruct("s1")
        s1.Members.Add(New NativeMember(
            "m1",
            New NativeTypeDef("foo")))
        VerifyTree(s1, "s1(m1(foo))")

        Dim transform As New NativeSymbolTransform
        transform.CollapseNamedTypes(s1)
        VerifyTree(s1, "s1(m1(foo))")
    End Sub

    ''' <summary>
    ''' Renaming a type is fun
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub TypeRename1()
        Dim s1 As New NativeStruct("s1")
        s1.Members.Add(New NativeMember(
            "m1",
            New NativeBuiltinType(BuiltinType.NativeChar)))

        Dim transform As New NativeSymbolTransform()
        transform.RenameTypeSymbol(s1, "s1", "s2")
        VerifyTree(s1, "s2(m1(char))")
    End Sub

    ''' <summary>
    ''' Rename through a typedef
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub TypeRename2()
        Dim s1 As New NativeStruct("s1")
        s1.Members.Add(New NativeMember(
            "m1",
            New NativeBuiltinType(BuiltinType.NativeChar)))
        Dim td As New NativeTypeDef("foo", s1)

        Dim transform As New NativeSymbolTransform()
        transform.RenameTypeSymbol(td, "s1", "s2")
        VerifyTree(td, "foo(s2(m1(char)))")
    End Sub

    ''' <summary>
    ''' Don't rename members
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub TypeRename3()
        Dim s1 As New NativeStruct("s1")
        s1.Members.Add(New NativeMember(
            "s1",
            New NativeBuiltinType(BuiltinType.NativeChar)))

        Dim transform As New NativeSymbolTransform()
        transform.RenameTypeSymbol(s1, "s1", "s2")
        VerifyTree(s1, "s2(s1(char))")
    End Sub

    ''' <summary>
    ''' Make sure named types get renamed as well
    ''' </summary>
    ''' <remarks></remarks>
    <Fact>
    Public Sub TypeRename4()
        Dim s1 As New NativeStruct("s1")
        s1.Members.Add(New NativeMember(
            "m1",
            New NativeNamedType("s1", New NativeBuiltinType(BuiltinType.NativeByte))))

        Dim transform As New NativeSymbolTransform()
        transform.RenameTypeSymbol(s1, "s1", "s2")
        VerifyTree(s1, "s2(m1(s2(byte)))")
    End Sub

End Class
